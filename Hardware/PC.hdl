// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Register(in=regin, load=loadin, out=countout, out=out);

    Inc16(in=countout, out=incout);
    Mux16(a=incout, b=in, sel=load, out=muxout); // load in or inc
    Or(a=load, b=inc, out=loadinpart); // init load bit

    Mux16(a=muxout, b=false, sel=reset, out=regin); // reset or load
    Or(a=loadinpart, b=reset, out=loadin); // init finalize load bit
}
